(ns translation-reader.screens.login
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/widgets.dart" :as widgets]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [translation-reader.services.auth :as auth-service]
   [translation-reader.widgets.auth :as auth-widget]
   [translation-reader.utils.util :as util]))
   
(defn ^m/Widget screen [ctx]
  (f/widget
   :get {navigator m/Navigator
         {{primary-color .-primary} .-colorScheme} m/Theme}
   :managed [email-controller (m/TextEditingController)
             password-controller (m/TextEditingController)]
   (m/Scaffold)
   .body
   (m/Padding
    .padding (m.EdgeInsets/only .top 40))
   (m/Center)
   (m/Container
    .width 400)
   (m/Padding
    .padding (.all m/EdgeInsets 20))
   (m/Column
    .children
    [(auth-widget/field email-controller "Email")
     (m/SizedBox .height 25)
     (auth-widget/field  password-controller "Password")
     (m/SizedBox .height 40)
     (f/widget
      (m/Container .height 100)
      (m/Row
       .children
       [(m/GestureDetector
         .onTap (fn []
                  (.pushNamed navigator "/signup")
                  nil)
         .child
         (mgl/MongolRichText
          .text (m/TextSpan
                 .text "Don't have an account?"
                 .style (m/TextStyle .fontSize 16)
                 .children [(m/TextSpan .text " Sign up"
                                        .style (m/TextStyle .color primary-color
                                                            .fontSize 16))])))
         (auth-widget/rounded-button (fn [] (auth-service/login
                                         ctx
                                         navigator
                                         {"email" (.-text email-controller)
                                          "password" (.-text password-controller)}
                                         (fn []
                                           (.popUntil navigator (widgets.ModalRoute/withName "/"))
                                           (util/show-toast ctx "yessss"))))
                                 "Done")]))])))